// This script contains functionality to allow bots to toss airstrikes from given waypoints

this.Name = "AIRSTRIKE";
this.Parent = "HighLevel";
this.SkipGetPriorityWhenActive = true;

this.LimitToWeapon( WEAPON.SMOKE_GRENADE );

this.Enter = function()
{
	if(!this.MarkInProgress()) { this.Finished(); }

	this.AddFinishCriteria(this.Bot.GetGameEntity(),"not weaponcharged", WEAPON.SMOKE_GRENADE);
};

this.GetPriority = function()
{
	while(1)
	{
		this.Priority = 0;
		if(this.Bot.IsWeaponCharged(WEAPON.SMOKE_GRENADE, 0))
		{
			Util.QueryGoal(this,0xc8998d3d /* AIRSTRIKE */);
		}

		sleep(2);
	}
};

this.Update = function()
{
	Util.RouteTo(this);

	if ( this.MapGoal.Stance == "crouch" )
	{
		this.Bot.HoldButton(BTN.CROUCH, 1.1);
	}

	this.AddAimRequest(Priority.High, "facing", Util.GetRandomTableValue(this.MapGoal.AimVectors));

	this.AddWeaponRequest(Priority.High, WEAPON.SMOKE_GRENADE );
	this.BlockForWeaponChange( WEAPON.SMOKE_GRENADE );
	this.Bot.HoldButton(BTN.ATTACK1, 0.5 );
	sleep(1);
	this.BlackboardDelay(5);

	this.Finished();
};

