this.Name = "DEFUSE";
this.Parent = "HighLevel";
this.SkipGetPriorityWhenActive = false; // cs: let them abort for higher priority defuses

this.LimitToWeapon(WEAPON.PLIERS);

this.Initialize = function()
{
	this.NoGibbing = GetCvar("g_botgib") == 0;
};

this.GetPriority = function()
{
	// cs: GetPriority seems to be called the same frame as this.Finished(). need to wait a frame
	//     so all the vars can be cleaned up. this.MapGoal is not null for example if goal is
	//     aborted for a higher priority one; which causes evaluation for the new goal to happen twice
	yield();

	while(1)
	{
		if ( this.QueryGoals(Util.QueryTable, 0x1899efc7 /* DEFUSE */) )
		{
			// always go for the closest one of the highest priority
			dist = null;
			evalDist = null;
			closestGoal = null;
			priority = 0;

			foreach ( goal in Util.QueryTable )
			{
				evalDist = this.Bot.DistanceTo(goal.GetEntity());
				evalPriority = goal.GetGoalPriority(this.Bot);

				if ( !dist || (evalDist < dist && evalPriority >= priority) ) {
					dist = evalDist;
					closestGoal = goal;
					priority = evalPriority;
				}
			}

			if ( closestGoal && closestGoal != this.MapGoal ) {
				// cs: this is temp. goals of the same type are not natively being aborted even for higher priority goals
				//     but i think that they should be.
				if ( this.IsActive() ) {
					dyno = this.MapGoal.GetEntity();
					// don't abort current goal if new dynamite is near previous dynamite
					if (!EntityIsValid(dyno) || Abs(this.Bot.DistanceTo(dyno) - dist) > 400) {
						//if (this.DebugAbortForHigherPriority) { this.Bot.Say("aborting defuse for higher priority"); }
						this.Finished();
					}
				} else {
					this.MapGoal = closestGoal;
					this.Priority = priority;
				}
			}
		}

		sleep(2);
	}
};

this.Enter = function()
{
	if(!this.MarkInProgress()) { this.Finished(); }

	this.TargetEntity = this.MapGoal.GetEntity();
};

this.Update = function()
{
	Util.RouteTo(this, 64);

	pos = GetEntPosition(this.TargetEntity);
	this.AddAimRequest(Priority.High, "position", pos);

	// when bot gib is disabled, humans are exploiting it by laying on top of dyno with /injure
	// so let's force a gib
	if (this.NoGibbing) {
		gibTarget = null;
		ent = GetEntityInSphere(this.Bot.GetPosition(), 300, CLASS.ANYPLAYER, gibTarget);
		dowhile(ent) {
			if ( ent && DistanceBetween(this.MapGoal.GetEntity(), ent) < 100 && !this.Bot.IsAllied(ent)
				&& !GetEntFlags(ent, ENTFLAG.LIMBO) ){
				gibTarget = ent;
				break;
			}
			ent = GetEntityInSphere(this.Bot.GetPosition(), 300, CLASS.ANYPLAYER, ent);
		}

		if (gibTarget) {
			this.AddWeaponRequest(Priority.High, this.Bot.GetCurrentWeapon() ); // don't reload
			this.AddAimRequest(Priority.High, "position", GetEntPosition(gibTarget));
			this.Bot.HoldButton(BTN.ATTACK1, 2);
			sleep(2);
			this.Finished(); // just reset the goal
		}
	}

	this.AddWeaponRequest(Priority.High, WEAPON.PLIERS );
	this.BlockForWeaponChange( WEAPON.PLIERS );

	while( this.TargetEntity && EntityIsValid(this.TargetEntity) ) { // goal deleted with entity
		pos = GetEntPosition(this.TargetEntity);
		if ( pos ) {
			this.AddAimRequest(Priority.High, "position", pos + Vector3(0,0,12));

			eyePos = this.Bot.GetEyePosition();
			heightDiff = pos.z - eyePos.z;
			if ( heightDiff > 20.f ) {
				this.Bot.PressButton(BTN.JUMP);
			}
			else {
				this.Bot.HoldButton(BTN.CROUCH, 0.5);
			}

			if ( this.Bot.DistanceTo(pos) > 64 ) {
				this.Bot.MoveTowards(pos);
			}

			this.Bot.HoldButton(BTN.ATTACK1, 0.25);
		}
		else {
			this.Finished();
		}
		sleep(0.25);
	}

	this.Finished();
};

this.Exit = function()
{
	this.TargetEntity = null;
	this.Bot.ReleaseButton(BTN.CROUCH);
	this.MapGoal = null;
};
