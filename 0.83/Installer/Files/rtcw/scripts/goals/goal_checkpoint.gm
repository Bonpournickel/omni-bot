this.Name = "CHECKPOINT";
this.Parent = "HighLevel";
this.SkipGetPriorityWhenActive = true;

this.GetPriority = function()
{
	while(1)
	{
		this.Priority = 0;
		if ( this.QueryGoals(this.Bot.QueryGoalsTable,0xf7e4a57f /* CHECKPOINT */) )
		{
			foreach ( goal in this.Bot.QueryGoalsTable )
			{
				// cs: if a flag is locked, it will be entflag.disabled
				if ( !GetEntFlags(goal.GetEntity(), ENTFLAG.DISABLED) ) {
					this.MapGoal = goal;
					this.Priority = goal.GetGoalPriority(this.Bot);
					break;
				}
			}
		}

		sleep(2);
	}
};

this.Enter = function()
{
	if(!this.MarkInProgress()) { this.Finished(); }
};

this.Update = function()
{
	Util.RouteTo(this);

	pos = this.MapGoal.GetPosition();
	this.AddAimRequest(Priority.Medium, "position", pos);

	timeout = 60; // 3 seconds

	while( timeout > 0 ) {
		this.Bot.MoveTowards(pos);
		yield();
		timeout -= 1;
	}

	if ( timeout < 1 ) {
		Util.ScriptDebugPrint(this.MapGoal.GetName(), "timedout");
		this.BlackboardDelay(10);
	}

	this.Finished();
};

